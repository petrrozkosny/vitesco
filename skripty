02 suma srazek mosnov = 
-- scenar 2: Respektuji filtry vizualu a pridavam dalsi,
-- tj. pouziji kombinaci funkci typu SUMX(FILTER()), MINX(FILTER())
SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
        --krok1: Funkce FILTER() prebira tabulku data filtrovanou vizualem
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec [lokalita]
        data[lokalita] = "MOSNOV"),
    --krok4: Funkce SUMX() scita sloupec [srazky] tabulky
    -- vracene funkci FILTER()
    data[srazky])
_____________________________________________-

03 suma srazek vsechny roky = 

-- scenar 3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji
-- kombinaci funkci SUMX(ALL()), MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
    ALL(data),
--krok2: Funkce SUMX() scita sloupec [srazky] tabulky vracene funkci ALL()
data[srazky])


___________________________________________________

04 suma srazek rok 2000 = 

-- scenar 4: Rusim filtry vizualu a nastavuji nove, tj. pouziji
-- kombinaci funkci SUMX(FILTER(ALL())), MINX(FILTER(ALL()))


SUMX(
    --krok2: Funkce FILTER() nastavuje filtr na tabulku vracenou funkci ALL()
    -- a nasledne vraci tabulku
    FILTER(
    --krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
    ALL(data),data[rok] = 2000),
    --krok3: Funkce SUMX() scita sloupec [srazky] tabulky vracene funkci
    -- FILTER()
    data[srazky])
    
    _________________________________________________________
    
    05 suma srazek kumulativne = 
-- YTD

SUMX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok])
    && data[mesic] <= SELECTEDVALUE(data[mesic])
    && data[lokalita] IN  VALUES(data[lokalita])
    ),
data[srazky])
_____________________________________
08 minimalni datum v danem roce mesici a lokalite = 
-- prebiram vsechny filtry vizualu, zadne nerusim, nenastavuji
MIN(data[datum])
_________________________________

09 minimalni datum roku mesice lokality kdy prselo = 
-- prselo znamena srazky vetsi 0
-- prebirame filtry vizualu, zadne nerusime, pridavame dalsi
MINX(FILTER(data,data[srazky] >0),data[datum])

_______________________________________

10 minimalni datum maximalnich srazek v roce mesici a lokalite = 

MINX(
    FILTER(data,
    -- rovna se maximalnim srazkam v danem roce, mesici, lokalite
    data[srazky] = MAX(data[srazky])),
data[datum])


_____________________________________________________________

11 kontrola srazek = 

-- Jsou maximalni srazky v danem roce,mesici a lokalite
-- maximalnimi srazkami za vsechny roky,mesice a lokalita
IF(
-- maximalni srazky v danem roce,mesici a lokalite 
MAX(data[srazky])
=
-- maximalni srazky za vsechny roky, mesice a lokality
MAXX(
    ALL(data),
data[srazky]),"Jsou maximalni","Nejsou maximalni")

____________________________________________

12 největší v každém roce napříč měsíci = 
-- maximální
-- výsledek bude "ano","ne"

VAR maximalni_srazky_mesice_roku_lokality = MAX(data[srazky])
VAR rok  = SELECTEDVALUE(data[rok])
VAR maximlani_srazky_roku_lokality =

MAXX(
    FILTER(
    ALL(data),
    data[rok] = rok
    && data[lokalita] = SELECTEDVALUE(data[lokalita])),
data[srazky])

RETURN IF(maximalni_srazky_mesice_roku_lokality = maximlani_srazky_roku_lokality,"ANO","NE")


____________________________________

14 max teplota barva = 

-- maximalni teplota v danem roce a lokalite
VAR max_rok_lokalita = MAX(data[maximalni_teplota])
-- maximalni teplota ve vsech letech a dane 
VAR max_lokalita = 

MAXX(
    FILTER(
    ALL(data),
    data[lokalita] IN VALUES(data[lokalita])),
data[maximalni_teplota])

RETURN
IF(max_rok_lokalita = max_lokalita,"#D7004B")

_________________________________________________

15 rok kontrola max teploty = 
-- jestli je maximalni teplota v danem roce vyssi nez vybrana hodnota
-- z posuvniku, tak "gold"

IF(MAX(data[maximalni_teplota]) > teplota[Hodnota teplota],"gold")
